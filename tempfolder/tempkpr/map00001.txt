LEVEL_VERSION(1)

REM -------------------------------------------------------------------------------------------------------------------------------------------

QUICK_MESSAGE(0, "Mentor, what news from the outside world?", PLAYER1)

CONCEAL_MAP_RECT(PLAYER1,128,128,252,252,1)

ADD_HEART_HEALTH(PLAYER1,-15000,0)

SET_GENERATE_SPEED(1000)

SET_DOOR_CONFIGURATION(WOOD,ManufactureRequired,6000)
SET_TRAP_CONFIGURATION(POISON_GAS,ManufactureRequired,8000)
SET_GAME_RULE(GoldPerGoldBlock,256)
SET_GAME_RULE(DungeonHeartHealHealth,0)

MAX_CREATURES(PLAYER1,16)

START_MONEY(PLAYER1,5000)

ADD_CREATURE_TO_POOL(FLY,6)
ADD_CREATURE_TO_POOL(BUG,24)
ADD_CREATURE_TO_POOL(SPIDER,2)

CREATURE_AVAILABLE(PLAYER1,FLY,1,0)
CREATURE_AVAILABLE(PLAYER1,BUG,1,0)
CREATURE_AVAILABLE(PLAYER1,SPIDER,1,0)

SET_CREATURE_MAX_LEVEL(PLAYER1,FLY,2)
SET_CREATURE_MAX_LEVEL(PLAYER1,BUG,2)
SET_CREATURE_MAX_LEVEL(PLAYER1,SPIDER,2)
SET_CREATURE_MAX_LEVEL(PLAYER1,IMP,2)

SET_CREATURE_MAX_LEVEL(PLAYER_GOOD,THIEF,2)
SET_CREATURE_MAX_LEVEL(PLAYER_GOOD,ARCHER,2)
SET_CREATURE_MAX_LEVEL(PLAYER_GOOD,DWARFA,2)
SET_CREATURE_MAX_LEVEL(PLAYER_GOOD,FAIRY,2)
SET_CREATURE_MAX_LEVEL(PLAYER_GOOD,WIZARD,2)
SET_CREATURE_MAX_LEVEL(PLAYER_GOOD,KNIGHT,3)
SET_CREATURE_MAX_LEVEL(PLAYER_GOOD,MONK,2)
SET_CREATURE_MAX_LEVEL(PLAYER_GOOD,BARBARIAN,2)

SET_CREATURE_HEALTH(KNIGHT,1250)
SET_CREATURE_ARMOUR(KNIGHT,60)
SET_CREATURE_STRENGTH(KNIGHT,25)
SET_CREATURE_CONFIGURATION(KNIGHT,BaseSpeed,30)

SET_CREATURE_HEALTH(BARBARIAN,800)
SET_CREATURE_ARMOUR(BARBARIAN,25)
SET_CREATURE_STRENGTH(BARBARIAN,30)

SET_CREATURE_HEALTH(THIEF,225)
SET_CREATURE_ARMOUR(THIEF,15)
SET_CREATURE_STRENGTH(THIEF,25)
SET_CREATURE_CONFIGURATION(THIEF,BaseSpeed,48)

SET_CREATURE_HEALTH(ARCHER,275)
SET_CREATURE_ARMOUR(ARCHER,15)
SET_CREATURE_STRENGTH(ARCHER,15)

SET_CREATURE_HEALTH(DWARFA,450)
SET_CREATURE_ARMOUR(DWARFA,30)
SET_CREATURE_STRENGTH(DWARFA,40)

ROOM_AVAILABLE(PLAYER1,TREASURE,1,1)
ROOM_AVAILABLE(PLAYER1,LAIR,1,1)
ROOM_AVAILABLE(PLAYER1,GARDEN,1,1)
ROOM_AVAILABLE(PLAYER1,TRAINING,1,1)

MAGIC_AVAILABLE(PLAYER1,POWER_HAND,1,1)
MAGIC_AVAILABLE(PLAYER1,POWER_SLAP,1,1)

DOOR_AVAILABLE(PLAYER1,WOOD,1,4)
TRAP_AVAILABLE(PLAYER1,POISON_GAS,1,0)

REM -------------------------------------------------------------------------------------------------------------------------------------------

CREATE_PARTY(THIEF_GUARD)
    ADD_TO_PARTY(THIEF_GUARD,THIEF,2,0,ATTACK_ENEMIES,8000)

CREATE_PARTY(ARCHER_GUARD)
    ADD_TO_PARTY(ARCHER_GUARD,ARCHER,2,0,ATTACK_ENEMIES,10000)
	
CREATE_PARTY(DWARF_GUARD)
    ADD_TO_PARTY(DWARF_GUARD,DWARFA,2,0,ATTACK_ENEMIES,12500)

CREATE_PARTY(FAIRY_GUARD)
    ADD_TO_PARTY(FAIRY_GUARD,FAIRY,2,0,STEAL_SPELLS,500)

CREATE_PARTY(SPELL_THIEVES)
    ADD_TO_PARTY(SPELL_THIEVES,FAIRY,2,0,STEAL_SPELLS,500)
	ADD_TO_PARTY(SPELL_THIEVES,WIZARD,2,0,STEAL_SPELLS,500)
	ADD_TO_PARTY(SPELL_THIEVES,MONK,2,0,STEAL_SPELLS,500)

CREATE_PARTY(THIEF_SNIPER)
	ADD_TO_PARTY(THIEF_SNIPER,THIEF,2,0,SNIPE_DUNGEON_HEART ,750)

CREATE_PARTY(FIRST_STRIKE)
    ADD_TO_PARTY(FIRST_STRIKE,THIEF,2,0,STEAL_GOLD,0)
    ADD_TO_PARTY(FIRST_STRIKE,THIEF,2,0,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(FIRST_STRIKE,THIEF,2,0,ATTACK_ROOMS,0)

CREATE_PARTY(SECOND_STRIKE)
    ADD_TO_PARTY(SECOND_STRIKE,THIEF,2,0,STEAL_GOLD,0)
    ADD_TO_PARTY(SECOND_STRIKE,THIEF,2,0,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(SECOND_STRIKE,ARCHER,2,0,DEFEND_PARTY,0)
    ADD_TO_PARTY(SECOND_STRIKE,THIEF,2,0,STEAL_GOLD,0)
    ADD_TO_PARTY(SECOND_STRIKE,ARCHER,2,0,DEFEND_PARTY,0)

CREATE_PARTY(THIRD_STRIKE)
    ADD_TO_PARTY(THIRD_STRIKE,THIEF,2,0,ATTACK_ROOMS,0)
    ADD_TO_PARTY(THIRD_STRIKE,THIEF,2,0,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(THIRD_STRIKE,THIEF,2,0,STEAL_GOLD,0)
    ADD_TO_PARTY(THIRD_STRIKE,ARCHER,2,0,DEFEND_PARTY,0)
    ADD_TO_PARTY(THIRD_STRIKE,ARCHER,2,0,ATTACK_ROOMS,0)
    ADD_TO_PARTY(THIRD_STRIKE,DWARFA,2,0,DEFEND_PARTY,0)

CREATE_PARTY(LORD)
    ADD_TO_PARTY(LORD,WIZARD,2,500,STEAL_SPELLS,1200)
    ADD_TO_PARTY(LORD,KNIGHT,3,5000,ATTACK_DUNGEON_HEART,1200)
    ADD_TO_PARTY(LORD,MONK,2,400,STEAL_GOLD,1200)
    ADD_TO_PARTY(LORD,ARCHER,2,300,DEFEND_PARTY,1200)
    ADD_TO_PARTY(LORD,BARBARIAN,2,200,ATTACK_ROOMS,1200)

REM -------------------------------------------------------------------------------------------------------------------------------------------

REM Music

RANDOMISE_FLAG(PLAYER_NEUTRAL,FLAG1,7)

IF(PLAYER_NEUTRAL,FLAG0 == 0)
	IF(PLAYER_NEUTRAL,FLAG1 == 1)
		IF(PLAYER1,ACTIVE_BATTLES >= 1)
			NEXT_COMMAND_REUSABLE
			SET_MUSIC("battle01.mp3")
			NEXT_COMMAND_REUSABLE
			SET_TIMER(PLAYER_NEUTRAL,TIMER0)
			NEXT_COMMAND_REUSABLE
			SET_FLAG(PLAYER_NEUTRAL,FLAG0,1)
		ENDIF
	ENDIF
ENDIF

IF(PLAYER_NEUTRAL,FLAG0 == 0)
	IF(PLAYER_NEUTRAL,FLAG1 == 2)
		IF(PLAYER1,ACTIVE_BATTLES >= 1)
			NEXT_COMMAND_REUSABLE
			SET_MUSIC("battle02.mp3")
			NEXT_COMMAND_REUSABLE
			SET_TIMER(PLAYER_NEUTRAL,TIMER0)
			NEXT_COMMAND_REUSABLE
			SET_FLAG(PLAYER_NEUTRAL,FLAG0,1)
		ENDIF
	ENDIF
ENDIF

IF(PLAYER_NEUTRAL,FLAG0 == 0)
	IF(PLAYER_NEUTRAL,FLAG1 == 3)
		IF(PLAYER1,ACTIVE_BATTLES >= 1)
			NEXT_COMMAND_REUSABLE
			SET_MUSIC("battle03.mp3")
			NEXT_COMMAND_REUSABLE
			SET_TIMER(PLAYER_NEUTRAL,TIMER0)
			NEXT_COMMAND_REUSABLE
			SET_FLAG(PLAYER_NEUTRAL,FLAG0,1)
		ENDIF
	ENDIF
ENDIF

IF(PLAYER_NEUTRAL,FLAG0 == 0)
	IF(PLAYER_NEUTRAL,FLAG1 == 4)
		IF(PLAYER1,ACTIVE_BATTLES >= 1)
			NEXT_COMMAND_REUSABLE
			SET_MUSIC("battle04.mp3")
			NEXT_COMMAND_REUSABLE
			SET_TIMER(PLAYER_NEUTRAL,TIMER0)
			NEXT_COMMAND_REUSABLE
			SET_FLAG(PLAYER_NEUTRAL,FLAG0,1)
		ENDIF
	ENDIF
ENDIF

IF(PLAYER_NEUTRAL,FLAG0 == 0)
	IF(PLAYER_NEUTRAL,FLAG1 == 5)
		IF(PLAYER1,ACTIVE_BATTLES >= 1)
			NEXT_COMMAND_REUSABLE
			SET_MUSIC("01. Theme.mp3")
			NEXT_COMMAND_REUSABLE
			SET_TIMER(PLAYER_NEUTRAL,TIMER0)
			NEXT_COMMAND_REUSABLE
			SET_FLAG(PLAYER_NEUTRAL,FLAG0,1)
		ENDIF
	ENDIF
ENDIF

IF(PLAYER_NEUTRAL,FLAG0 == 0)
	IF(PLAYER_NEUTRAL,FLAG1 == 6)
		IF(PLAYER1,ACTIVE_BATTLES >= 1)
			NEXT_COMMAND_REUSABLE
			SET_MUSIC("05. Outro Fail.mp3")
			NEXT_COMMAND_REUSABLE
			SET_TIMER(PLAYER_NEUTRAL,TIMER0)
			NEXT_COMMAND_REUSABLE
			SET_FLAG(PLAYER_NEUTRAL,FLAG0,1)
		ENDIF
	ENDIF
ENDIF

IF(PLAYER_NEUTRAL,FLAG0 == 0)
	IF(PLAYER_NEUTRAL,FLAG1 == 7)
		IF(PLAYER1,ACTIVE_BATTLES >= 1)
			NEXT_COMMAND_REUSABLE
			SET_MUSIC("06. Outro Win.mp3")
			NEXT_COMMAND_REUSABLE
			SET_TIMER(PLAYER_NEUTRAL,TIMER0)
			NEXT_COMMAND_REUSABLE
			SET_FLAG(PLAYER_NEUTRAL,FLAG0,1)
		ENDIF
	ENDIF
ENDIF

IF(PLAYER_NEUTRAL,FLAG0 == 1)
	IF(PLAYER_NEUTRAL,TIMER0 >= 600)
		IF(PLAYER1,ACTIVE_BATTLES == 0)	
			NEXT_COMMAND_REUSABLE
			RANDOMISE_FLAG(PLAYER_NEUTRAL,FLAG1,7)
			NEXT_COMMAND_REUSABLE
			SET_FLAG(PLAYER_NEUTRAL,FLAG0,0)
			NEXT_COMMAND_REUSABLE
			RANDOMISE_FLAG(PLAYER_NEUTRAL,FLAG2,4)
			NEXT_COMMAND_REUSABLE
			SET_TIMER(PLAYER_NEUTRAL,TIMER1)
			NEXT_COMMAND_REUSABLE
			ADD_TO_TIMER(PLAYER_NEUTRAL,TIMER1,6600)
		ENDIF
	ENDIF
ENDIF

IF(PLAYER_NEUTRAL,FLAG0 == 0)
	IF(PLAYER_NEUTRAL,FLAG2 == 1)
		IF(PLAYER_NEUTRAL,TIMER1 >= 6600)
			NEXT_COMMAND_REUSABLE
			SET_MUSIC(3)
			NEXT_COMMAND_REUSABLE
			ADD_TO_FLAG(PLAYER_NEUTRAL,FLAG2,1)
			NEXT_COMMAND_REUSABLE
			SET_TIMER(PLAYER_NEUTRAL,TIMER1)
		ENDIF
	ENDIF
ENDIF

IF(PLAYER_NEUTRAL,FLAG0 == 0)
	IF(PLAYER_NEUTRAL,FLAG2 == 2)
		IF(PLAYER_NEUTRAL,TIMER1 >= 6600)
			NEXT_COMMAND_REUSABLE
			SET_MUSIC(4)
			NEXT_COMMAND_REUSABLE
			ADD_TO_FLAG(PLAYER_NEUTRAL,FLAG2,1)
			NEXT_COMMAND_REUSABLE
			SET_TIMER(PLAYER_NEUTRAL,TIMER1)
		ENDIF
	ENDIF
ENDIF

IF(PLAYER_NEUTRAL,FLAG0 == 0)
	IF(PLAYER_NEUTRAL,FLAG2 == 3)
		IF(PLAYER_NEUTRAL,TIMER1 >= 6600)
			NEXT_COMMAND_REUSABLE
			SET_MUSIC(5)
			NEXT_COMMAND_REUSABLE
			ADD_TO_FLAG(PLAYER_NEUTRAL,FLAG2,1)
			NEXT_COMMAND_REUSABLE
			SET_TIMER(PLAYER_NEUTRAL,TIMER1)
		ENDIF
	ENDIF
ENDIF

IF(PLAYER_NEUTRAL,FLAG0 == 0)
	IF(PLAYER_NEUTRAL,FLAG2 == 4)
		IF(PLAYER_NEUTRAL,TIMER1 >= 6600)
			NEXT_COMMAND_REUSABLE
			SET_MUSIC(6)
			NEXT_COMMAND_REUSABLE
			ADD_TO_FLAG(PLAYER_NEUTRAL,FLAG2,-3)
			NEXT_COMMAND_REUSABLE
			SET_TIMER(PLAYER_NEUTRAL,TIMER1)
		ENDIF
	ENDIF
ENDIF

REM -------------------------------------------------------------------------------------------------------------------------------------------

IF(PLAYER1,GAME_TURN >= 250)
	QUICK_OBJECTIVE(1,​"Keeper, the underground of Summerland stirs. More of The Sleepers are waking up and now it is your turn. Regarding the immediate situation: a minor Keeper by the name of Mara is attacking neighboring Flameburgh and renewed activities of the White Horde on the Silent Plains have drawn the majority of Heroes away from this realm. Use this opportunity to quickly break out of this prison and attract the few available Creatures. ",PLAYER1)
	TUTORIAL_FLASH_BUTTON(40,0)
ENDIF

IF(PLAYER1,GAME_TURN >= 3000)
	IF(PLAYER1,BATTLES_WON >= 3)
		QUICK_OBJECTIVE(2,​"Thread carefully, Keeper. The Heroes have learned that weakening and subsequently containing Keepers is better than destroying their Dungeon Heart which would allow them to regenerate and eventually return. Do not allow them to damage your Dungeon Heart any further.  ",PLAYER1)
	ENDIF
ENDIF

IF(PLAYER1,GAME_TURN >= 9000)
	IF(PLAYER1,BATTLES_WON >= 10)
		QUICK_OBJECTIVE(3,​"Keeper, whilst the forces of evil in Flameburgh and the Silent Plains may be an excellent distraction it would be unwise to declare yourself openly for the time being. Otherwise this will undoubtly provoke a response from the Guardians. Leave no survivors to tell the tale. ",PLAYER1)
	ENDIF
ENDIF

IF_ACTION_POINT(1,PLAYER1)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,THIEF_GUARD,2,1)
	QUICK_MESSAGE(4, "Alarm! The Blue Tempest broke out!", THIEF)
ENDIF

IF_ACTION_POINT(3,PLAYER1)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,THIEF_GUARD,4,1)
ENDIF

IF_ACTION_POINT(5,PLAYER1)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,THIEF_GUARD,6,1)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,ARCHER_GUARD,7,1)
	QUICK_MESSAGE(5, "Alarm! Inform the Warden Commander at once!", ARCHER)
ENDIF

IF_ACTION_POINT(8,PLAYER1)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,ARCHER_GUARD,9,1)
	QUICK_MESSAGE(6, "Stop! Stop right there!", ARCHER)
ENDIF

IF_ACTION_POINT(10,PLAYER1)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,THIEF_GUARD,11,1)
ENDIF

IF_ACTION_POINT(12,PLAYER1)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,THIEF_GUARD,13,1)
ENDIF

IF_ACTION_POINT(14,PLAYER1)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,FAIRY_GUARD,16,2)
ENDIF

IF_ACTION_POINT(15,PLAYER1)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,FAIRY_GUARD,16,2)
ENDIF

IF(PLAYER1,BRIDGE > 1)
	ROOM_AVAILABLE(ALL_PLAYERS,BRIDGE,1,1)
	TUTORIAL_FLASH_BUTTON(18,100)
ENDIF

IF(PLAYER1,MONEY <= 2500)
	QUICK_INFORMATION(7,​"Your gold reserves are running low. Gold can be mined in the immediate surrounding earth of this dungeon.",PLAYER1)
	REVEAL_MAP_RECT(PLAYER1,33,42,7,7)
	REVEAL_MAP_RECT(PLAYER1,111,45,7,7)
	REVEAL_MAP_RECT(PLAYER1,81,66,7,7)
	REVEAL_MAP_RECT(PLAYER1,55,109,7,7)
	REVEAL_MAP_RECT(PLAYER1,219,46,7,7)
	REVEAL_MAP_RECT(PLAYER1,153,59,7,7)
	REVEAL_MAP_RECT(PLAYER1,178,88,7,7)
	REVEAL_MAP_RECT(PLAYER1,201,108,7,7)
	REVEAL_MAP_RECT(PLAYER1,40,222,7,7)
	REVEAL_MAP_RECT(PLAYER1,57,149,7,7)
	REVEAL_MAP_RECT(PLAYER1,78,166,7,7)
	REVEAL_MAP_RECT(PLAYER1,105,200,7,7)
	REVEAL_MAP_RECT(PLAYER1,198,150,7,7)
	REVEAL_MAP_RECT(PLAYER1,189,165,7,7)
	REVEAL_MAP_RECT(PLAYER1,147,204,7,7)
	REVEAL_MAP_RECT(PLAYER1,219,221,7,7)
ENDIF

IF(PLAYER1,TOTAL_CREATURES >= 10)
	IF(PLAYER1,TOTAL_GOLD_MINED >= 5000)
		IF(PLAYER1,TOTAL_AREA >= 180)
			ADD_PARTY_TO_LEVEL(PLAYER_GOOD,FIRST_STRIKE,-1,1)
			ADD_PARTY_TO_LEVEL(PLAYER_GOOD,FIRST_STRIKE,-2,1)
			ADD_PARTY_TO_LEVEL(PLAYER_GOOD,THIEF_GUARD,-2,1)
			ADD_PARTY_TO_LEVEL(PLAYER_GOOD,DWARF_GUARD,-2,1)
			ADD_PARTY_TO_LEVEL(PLAYER_GOOD,ARCHER_GUARD,-2,1)
			ADD_PARTY_TO_LEVEL(PLAYER_GOOD,THIEF_GUARD,-1,1)
			ADD_PARTY_TO_LEVEL(PLAYER_GOOD,ARCHER_GUARD,-1,1)
			ADD_PARTY_TO_LEVEL(PLAYER_GOOD,DWARF_GUARD,-1,1)
			SET_FLAG(PLAYER1,FLAG0,1)
			SET_TIMER(PLAYER1,TIMER0)
			QUICK_OBJECTIVE(8,​"You are doing well so far. Just be aware that the local Heroes have become aware of your activities, it took them long enough. Prepare for counterattacks.",PLAYER1)
		ENDIF
	ENDIF
ENDIF

IF(PLAYER1,TIMER0 >= 2500)
    IF(PLAYER1,FLAG0 == 1)
        NEXT_COMMAND_REUSABLE
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,FIRST_STRIKE,-1,1)
		NEXT_COMMAND_REUSABLE
		ADD_PARTY_TO_LEVEL(PLAYER_GOOD,THIEF_SNIPER,DRAWFROM(16,17),1)
        NEXT_COMMAND_REUSABLE
        SET_TIMER(PLAYER1,TIMER0)
    ENDIF
ENDIF

IF(PLAYER1,TOTAL_CREATURES >= 16)
	IF(PLAYER1,BATTLES_WON >= 35)
		IF(PLAYER1,TOTAL_GOLD_MINED >= 5000)
			SET_FLAG(PLAYER1,FLAG0,2)
			SET_TIMER(PLAYER1,TIMER1)
			ADD_PARTY_TO_LEVEL(PLAYER_GOOD,FIRST_STRIKE,-2,1)
			ADD_PARTY_TO_LEVEL(PLAYER_GOOD,THIEF_SNIPER,DRAWFROM(16,17),1)
		ENDIF
	ENDIF
ENDIF

IF(PLAYER1,TIMER1 >= 3000)
    IF(PLAYER1,FLAG0 == 2)
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,SECOND_STRIKE,-1,1)
		ADD_PARTY_TO_LEVEL(PLAYER_GOOD,THIEF_SNIPER,DRAWFROM(16,17),1)
    ENDIF
ENDIF

IF(PLAYER1,TIMER1 >= 5750)
    IF(PLAYER1,FLAG0 == 2)
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,SECOND_STRIKE,-2,1)
		ADD_PARTY_TO_LEVEL(PLAYER_GOOD,THIEF_SNIPER,DRAWFROM(16,17),1)
    ENDIF
ENDIF

IF(PLAYER1,TIMER1 >= 8300)
    IF(PLAYER1,FLAG0 == 2)
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,THIRD_STRIKE,-2,1)
		ADD_PARTY_TO_LEVEL(PLAYER_GOOD,THIEF_SNIPER,DRAWFROM(16,17),1)
    ENDIF
ENDIF

IF(PLAYER1,TIMER1 >= 10500)
    IF(PLAYER1,FLAG0 == 2)
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,THIRD_STRIKE,-1,1)
		ADD_PARTY_TO_LEVEL(PLAYER_GOOD,THIEF_SNIPER,DRAWFROM(16,17),1)
		SET_FLAG(PLAYER1,FLAG0,3)
		SET_TIMER(PLAYER1,TIMER2)
    ENDIF
ENDIF

IF_AVAILABLE(PLAYER1,POWER_SPEED >= 1)
	IF(PLAYER_GOOD,TOTAL_CREATURES <= 10)
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD,SPELL_THIEVES,-2,1)
	ENDIF
ENDIF

IF_AVAILABLE(PLAYER1,POWER_HEAL_CREATURE >= 1)
	IF(PLAYER_GOOD,TOTAL_CREATURES <= 10)
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD,SPELL_THIEVES,-1,1)
	ENDIF
ENDIF

IF(PLAYER1,TIMER2 >= 1500)
    IF(PLAYER1,FLAG0 == 3)
		ADD_PARTY_TO_LEVEL(PLAYER_GOOD,THIRD_STRIKE,-2,1)
		QUICK_INFORMATION(9,​"The Warden Commander is approaching the area.",PLAYER1)
		SET_GENERATE_SPEED(500)
		HIDE_HERO_GATE(-2,1)
    ENDIF
ENDIF

IF(PLAYER1,TIMER2 >= 4500)
    IF(PLAYER1,FLAG0 == 3)
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,LORD,-1,1)
		QUICK_MESSAGE(10, "Keeper, this stops now.", Knight)
        SET_FLAG(PLAYER1,FLAG0,4)
		HIDE_HERO_GATE(-1,1)
		ZOOM_TO_LOCATION(PLAYER1,-1)
    ENDIF
ENDIF

IF(PLAYER1,FLAG0 == 4)
	IF(PLAYER_GOOD,TOTAL_CREATURES == 0)
		WIN_GAME
		SET_FLAG(PLAYER1,FLAG0,5)
		QUICK_OBJECTIVE(11,​"Your tenacity was the deciding factor for victory here. This is a trait you will definitely need the coming time with everything ongoing now in Summerland. For now, let us introduce the local inhabitants to your insect minions.",PLAYER1)
    ENDIF
ENDIF

IF(PLAYER1,HEART_HEALTH <= 2500)
	ALLY_PLAYERS(PLAYER1,PLAYER_GOOD,3)
	LOSE_GAME
ENDIF

RUN_AFTER_VICTORY(1)

IF(PLAYER1,FLAG0 == 5)
	CHANGE_SLAB_TYPE(​5,​66,​DIRT)
ENDIF
