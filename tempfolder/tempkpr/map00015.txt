LEVEL_VERSION(1)

REM -------------------------------------------------------------------------------------------------------------------------------------------

COMPUTER_PLAYER(PLAYER3,0)
COMPUTER_PLAYER(PLAYER5,ROAMING)
SET_PLAYER_COLOR(PLAYER3,WHITE)

ALLY_PLAYERS(PLAYER1,PLAYER3,2)
ALLY_PLAYERS(PLAYER3,PLAYER_GOOD,1)

START_MONEY(PLAYER1,25000)
START_MONEY(PLAYER3,500000)
START_MONEY(PLAYER_GOOD,200000)

CONCEAL_MAP_RECT(PLAYER1,229, 136, 3, 3, 1)

SET_CREATURE_HEALTH(WITCH,5000)
SET_CREATURE_STRENGTH(WITCH,150)
SET_CREATURE_ARMOUR(WITCH,170)
SET_CREATURE_PROPERTY(WITCH,NEVER_CHICKENS,1)
SET_CREATURE_PROPERTY(WITCH,IMMUNE_TO_BOULDER,1)
SET_CREATURE_PROPERTY(WITCH,ONE_OF_KIND,1)
SET_CREATURE_PROPERTY(WITCH,UNAFFECTED_BY_WIND,1)
SET_CREATURE_PROPERTY(WITCH,ILLUMINATED,1)
SET_CREATURE_CONFIGURATION(WITCH,BaseSpeed,30)
SET_CREATURE_PROPERTY(WITCH,NO_IMPRISONMENT,1)
SET_CREATURE_CONFIGURATION(WITCH,LUCK,30)
SET_CREATURE_CONFIGURATION(WITCH,FearsomeFactor,10)

SET_CREATURE_INSTANCE(WITCH, 2, CAST_SPELL_CHICKEN, 2)
SET_CREATURE_INSTANCE(WITCH, 6, FREEZE, 6)
SET_CREATURE_INSTANCE(WITCH, 9, FIRE_BOMB, 9)
SET_CREATURE_INSTANCE(WITCH, 10, WORD_OF_POWER, 10)

SET_CREATURE_INSTANCE(SAMURAI, 10, INVISIBILITY, 10)
SET_CREATURE_FEAR_WOUNDED(SAMURAI,0)

REM Mystic Knights
SET_CREATURE_HEALTH(AVATAR,850)
SET_CREATURE_ARMOUR(AVATAR,65)
SET_CREATURE_STRENGTH(AVATAR,75)
SET_CREATURE_PROPERTY(AVATAR,ONE_OF_KIND,0)
SET_CREATURE_PROPERTY(AVATAR,LORD,0)
SET_CREATURE_PROPERTY(AVATAR,NEVER_CHICKENS,0)
SET_CREATURE_PROPERTY(AVATAR,IMMUNE_TO_BOULDER,0)
SET_CREATURE_PROPERTY(AVATAR,ONE_OF_KIND,0)
SET_CREATURE_PROPERTY(AVATAR,NO_IMPRISONMENT,1)

SET_CREATURE_CONFIGURATION(TUNNELLER,BaseSpeed,80)
SET_CREATURE_CONFIGURATION(TUNNELLER,PAY,0)

SET_CREATURE_PROPERTY(KNIGHT,LORD,0)

REM Elite Vampire Kessler
SET_CREATURE_HEALTH(VAMPIRE,1000)
SET_CREATURE_STRENGTH(VAMPIRE,80)
SET_CREATURE_ARMOUR(VAMPIRE,40)
SET_CREATURE_CONFIGURATION(VAMPIRE,LUCK,30)

SET_COMPUTER_GLOBALS(PLAYER3,50,15,1,1,2000,5)
SET_COMPUTER_PROCESS(PLAYER3,"ATTACK SAFE ATTACK",-1,100,335,100,0)
SET_COMPUTER_PROCESS(PLAYER3,"ATTACK PLAN 1",-1,100,335,100,0)
SET_CREATURE_TENDENCIES(PLAYER3,FLEE,1)

	SET_PLAYER_MODIFIER(PLAYER3,SALARY,75)
	SET_PLAYER_MODIFIER(PLAYER3,TrainingCost,75)

SET_HAND_RULE(PLAYER3,ANY_CREATURE,RULE0,DENY,LEVEL_HIGHER,5)
SET_HAND_RULE(PLAYER3,ANY_CREATURE,RULE1,DENY,AGE_HIGHER,30)

SET_GENERATE_SPEED(600)

MAX_CREATURES(ALL_PLAYERS,20)

ADD_CREATURE_TO_POOL(TROLL,17)
ADD_CREATURE_TO_POOL(SORCEROR,17)
ADD_CREATURE_TO_POOL(BILE_DEMON,12)
ADD_CREATURE_TO_POOL(DARK_MISTRESS,5)
ADD_CREATURE_TO_POOL(DRAGON,30)
ADD_CREATURE_TO_POOL(TENTACLE,30)
ADD_CREATURE_TO_POOL(ORC,17)
ADD_CREATURE_TO_POOL(HELL_HOUND,20)
ADD_CREATURE_TO_POOL(DEMONSPAWN,20)
ADD_CREATURE_TO_POOL(BUG,10)
ADD_CREATURE_TO_POOL(FLY,10)
ADD_CREATURE_TO_POOL(SPIDER,10)
ADD_CREATURE_TO_POOL(HORNY,1)

ADD_CREATURE_TO_POOL(DWARFA,4)
ADD_CREATURE_TO_POOL(ARCHER,10)
ADD_CREATURE_TO_POOL(THIEF,4)
ADD_CREATURE_TO_POOL(FAIRY,10)
ADD_CREATURE_TO_POOL(MONK,10)
ADD_CREATURE_TO_POOL(BARBARIAN,10)
ADD_CREATURE_TO_POOL(GIANT,10)
ADD_CREATURE_TO_POOL(KNIGHT,15)
ADD_CREATURE_TO_POOL(AVATAR,6)
ADD_CREATURE_TO_POOL(WIZARD,12)
ADD_CREATURE_TO_POOL(SAMURAI,10)
ADD_CREATURE_TO_POOL(TIME_MAGE,2)

ROOM_AVAILABLE(PLAYER1,TREASURE,1,1)
ROOM_AVAILABLE(PLAYER1,LAIR,1,1)
ROOM_AVAILABLE(PLAYER1,GARDEN,1,1)
ROOM_AVAILABLE(PLAYER1,TRAINING,1,1)
ROOM_AVAILABLE(PLAYER1,RESEARCH,1,1)
ROOM_AVAILABLE(PLAYER1,WORKSHOP,1,0)
ROOM_AVAILABLE(PLAYER1,TORTURE,1,0)
ROOM_AVAILABLE(PLAYER1,BARRACKS,1,0)

CREATURE_AVAILABLE(PLAYER1,TROLL,1,0)
CREATURE_AVAILABLE(PLAYER1,SORCEROR,1,0)
CREATURE_AVAILABLE(PLAYER1,BILE_DEMON,1,0)
CREATURE_AVAILABLE(PLAYER1,DARK_MISTRESS,1,0)
CREATURE_AVAILABLE(PLAYER1,DRAGON,1,0)
CREATURE_AVAILABLE(PLAYER1,TENTACLE,1,0)
CREATURE_AVAILABLE(PLAYER1,ORC,1,0)
CREATURE_AVAILABLE(PLAYER1,HELL_HOUND,1,0)
CREATURE_AVAILABLE(PLAYER1,BUG,1,0)
CREATURE_AVAILABLE(PLAYER1,FLY,1,0)
CREATURE_AVAILABLE(PLAYER1,HORNY,1,0)


MAGIC_AVAILABLE(PLAYER1,POWER_HAND,1,1)
MAGIC_AVAILABLE(PLAYER3,POWER_HAND,1,1)
MAGIC_AVAILABLE(PLAYER1,POWER_IMP,1,1)
MAGIC_AVAILABLE(PLAYER1,POWER_SLAP,1,1)
MAGIC_AVAILABLE(PLAYER3,POWER_SLAP,1,1)
MAGIC_AVAILABLE(PLAYER3,POWER_SIGHT,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_OBEY,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_CONCEAL,1,0)
MAGIC_AVAILABLE(PLAYER1,POWER_CALL_TO_ARMS,1,0)
MAGIC_AVAILABLE(PLAYER3,POWER_CALL_TO_ARMS,1,1)
MAGIC_AVAILABLE(PLAYER1,POWER_HEAL_CREATURE,1,0)
MAGIC_AVAILABLE(PLAYER1,POWER_PROTECT,1,0)
MAGIC_AVAILABLE(PLAYER3,POWER_CONCEAL,1,0)
MAGIC_AVAILABLE(PLAYER3,POWER_CAVE_IN,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_HOLD_AUDIENCE,1,0)
MAGIC_AVAILABLE(PLAYER_GOOD,POWER_CHICKEN,1,1)
MAGIC_AVAILABLE(PLAYER_GOOD,POWER_SPEED,1,1)
MAGIC_AVAILABLE(PLAYER1,POWER_SPEED,1,0)

TRAP_AVAILABLE(PLAYER3,BOULDER,1,0)
TRAP_AVAILABLE(ALL_PLAYERS,ALARM,1,0)
TRAP_AVAILABLE(ALL_PLAYERS,POISON_GAS,1,0)
DOOR_AVAILABLE(ALL_PLAYERS,STEEL,1,0)
DOOR_AVAILABLE(ALL_PLAYERS,MAGIC,1,0)
TRAP_AVAILABLE(ALL_PLAYERS,LIGHTNING,1,0)
TRAP_AVAILABLE(ALL_PLAYERS,WORD_OF_POWER,1,0)

REM -------------------------------------------------------------------------------------------------------------------------------------------

CREATE_PARTY(BLUE_ONE)
    ADD_TO_PARTY(BLUE_ONE,DWARFA,4,200,ATTACK_ROOMS,0)
    ADD_TO_PARTY(BLUE_ONE,BARBARIAN,5,200,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(BLUE_ONE,THIEF,5,100,STEAL_GOLD,0)
    ADD_TO_PARTY(BLUE_ONE,DWARFA,5,200,ATTACK_ROOMS,0)
    ADD_TO_PARTY(BLUE_ONE,ARCHER,4,300,DEFEND_PARTY,0)
    ADD_TO_PARTY(BLUE_ONE,ARCHER,4,300,DEFEND_PARTY,0)

CREATE_PARTY(BLUE_TWO)
    ADD_TO_PARTY(BLUE_TWO,BARBARIAN,7,300,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(BLUE_TWO,BARBARIAN,6,400,ATTACK_ROOMS,0)
    ADD_TO_PARTY(BLUE_TWO,GIANT,6,200,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(BLUE_TWO,MONK,6,300,STEAL_SPELLS,0)
    ADD_TO_PARTY(BLUE_TWO,ARCHER,7,300,DEFEND_PARTY,0)

CREATE_PARTY(BLUE_THREE)
	ADD_TO_PARTY(BLUE_THREE,WIZARD,9,700,STEAL_SPELLS,0)
    ADD_TO_PARTY(BLUE_THREE,SAMURAI,9,600,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(BLUE_THREE,GIANT,10,500,STEAL_SPELLS,0)
    ADD_TO_PARTY(BLUE_THREE,MONK,8,500,STEAL_SPELLS,0)
    ADD_TO_PARTY(BLUE_THREE,BARBARIAN,8,500,ATTACK_ROOMS,0)

CREATE_PARTY(WITCH_QUEEN)
    ADD_TO_PARTY(WITCH_QUEEN,WITCH,10,10000,DEFEND_HEART,150000)

CREATE_PARTY(RAIDERS)
    ADD_TO_PARTY(RAIDERS,BARBARIAN,5,400,ATTACK_ROOMS,0)
    ADD_TO_PARTY(RAIDERS,BARBARIAN,6,400,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(RAIDERS,ARCHER,6,300,STEAL_SPELLS,0)
    ADD_TO_PARTY(RAIDERS,DWARFA,5,200,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(RAIDERS,THIEF,5,100,STEAL_GOLD,0)

CREATE_PARTY(HOLY)
    ADD_TO_PARTY(HOLY,MONK,8,500,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(HOLY,WIZARD,8,400,STEAL_SPELLS,0)
    ADD_TO_PARTY(HOLY,SAMURAI,7,500,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(HOLY,SAMURAI,7,500,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(HOLY,MONK,7,200,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(HOLY,MONK,7,200,ATTACK_ENEMIES,0)

CREATE_PARTY(ELITE)
    ADD_TO_PARTY(ELITE,SAMURAI,9,1000,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(ELITE,WIZARD,10,1500,STEAL_SPELLS,0)
    ADD_TO_PARTY(ELITE,BARBARIAN,8,1000,STEAL_GOLD,0)
    ADD_TO_PARTY(ELITE,GIANT,8,1000,ATTACK_ROOMS,0)
    ADD_TO_PARTY(ELITE,ARCHER,9,800,DEFEND_PARTY,0)
    ADD_TO_PARTY(ELITE,SAMURAI,8,1000,DEFEND_PARTY,0)

CREATE_PARTY(SCOUTS)
    ADD_TO_PARTY(SCOUTS,DWARFA,10,500,ATTACK_ROOMS,0)
    ADD_TO_PARTY(SCOUTS,THIEF,9,100,STEAL_GOLD,0)
    ADD_TO_PARTY(SCOUTS,THIEF,9,100,STEAL_GOLD,0)
    ADD_TO_PARTY(SCOUTS,THIEF,8,100,STEAL_GOLD,0)
    ADD_TO_PARTY(SCOUTS,ARCHER,9,300,DEFEND_PARTY,0)
    ADD_TO_PARTY(SCOUTS,ARCHER,8,200,DEFEND_PARTY,0)
    ADD_TO_PARTY(SCOUTS,ARCHER,8,200,DEFEND_PARTY,0)

CREATE_PARTY(RPG)
    ADD_TO_PARTY(RPG,WIZARD,10,1000,STEAL_SPELLS,1600)
    ADD_TO_PARTY(RPG,KNIGHT,10,1500,ATTACK_DUNGEON_HEART,1600)
	ADD_TO_PARTY(RPG,AVATAR,9,1500,DEFEND_PARTY,1600)
    ADD_TO_PARTY(RPG,MONK,9,1000,STEAL_SPELLS,1600)
    ADD_TO_PARTY(RPG,BARBARIAN,9,1000,ATTACK_ENEMIES,1600)
    ADD_TO_PARTY(RPG,SAMURAI,9,1000,DEFEND_PARTY,1600)
    ADD_TO_PARTY(RPG,ARCHER,9,800,DEFEND_PARTY,1600)

CREATE_PARTY(BRUTES)
    ADD_TO_PARTY(BRUTES,BARBARIAN,9,400,DEFEND_PARTY,0)
    ADD_TO_PARTY(BRUTES,GIANT,9,600,STEAL_SPELLS,0)
    ADD_TO_PARTY(BRUTES,GIANT,8,500,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(BRUTES,BARBARIAN,8,400,DEFEND_PARTY,0)

CREATE_PARTY(GIANT_LIGHT)
	ADD_TO_PARTY(GIANT_LIGHT,GIANT,5,0,ATTACK_ROOMS,0)
	
CREATE_PARTY(FAIRY_LIGHT)
	ADD_TO_PARTY(FAIRY_LIGHT,FAIRY,5,0,STEAL_SPELLS,0)

CREATE_PARTY(GIANT_ATTACKER)
	ADD_TO_PARTY(GIANT_ATTACKER,GIANT,9,0,ATTACK_ROOMS,0)

CREATE_PARTY(GIANT_DESTROYER)
	ADD_TO_PARTY(GIANT_DESTROYER,GIANT,10,0,SABOTAGE_ROOMS,0)

CREATE_PARTY(FAIRY_GOLD_THIEF)
	ADD_TO_PARTY(FAIRY_GOLD_THIEF,FAIRY,9,0,STEAL_GOLD,0)

CREATE_PARTY(FAIRY_SPELL_THIEF)
	ADD_TO_PARTY(FAIRY_SPELL_THIEF,FAIRY,10,0,STEAL_SPELLS,0)

CREATE_PARTY(THIEF_SPELL)
	ADD_TO_PARTY(THIEF_SPELL,THIEF,10,0,STEAL_SPELLS,0)

CREATE_PARTY(THIEF_GOLD)
	ADD_TO_PARTY(THIEF_GOLD,THIEF,10,0,STEAL_GOLD,0)
	
CREATE_PARTY(ASSASSIN)
	ADD_TO_PARTY(ASSASSIN,SAMURAI,10,500,SNIPE_DUNGEON_HEART,0)
	
CREATE_PARTY(SPELL_THIEF)
	ADD_TO_PARTY(SPELL_THIEF,TIME_MAGE,10,500,STEAL_SPELLS,0)

REM -------------------------------------------------------------------------------------------------------------------------------------------

REM Music

RANDOMISE_FLAG(PLAYER_NEUTRAL,FLAG1,7)

IF(PLAYER_NEUTRAL,FLAG0 == 0)
	IF(PLAYER_NEUTRAL,FLAG1 == 1)
		IF(PLAYER1,ACTIVE_BATTLES >= 1)
			NEXT_COMMAND_REUSABLE
			SET_MUSIC("battle01.mp3")
			NEXT_COMMAND_REUSABLE
			SET_TIMER(PLAYER_NEUTRAL,TIMER0)
			NEXT_COMMAND_REUSABLE
			SET_FLAG(PLAYER_NEUTRAL,FLAG0,1)
		ENDIF
	ENDIF
ENDIF

IF(PLAYER_NEUTRAL,FLAG0 == 0)
	IF(PLAYER_NEUTRAL,FLAG1 == 2)
		IF(PLAYER1,ACTIVE_BATTLES >= 1)
			NEXT_COMMAND_REUSABLE
			SET_MUSIC("battle02.mp3")
			NEXT_COMMAND_REUSABLE
			SET_TIMER(PLAYER_NEUTRAL,TIMER0)
			NEXT_COMMAND_REUSABLE
			SET_FLAG(PLAYER_NEUTRAL,FLAG0,1)
		ENDIF
	ENDIF
ENDIF

IF(PLAYER_NEUTRAL,FLAG0 == 0)
	IF(PLAYER_NEUTRAL,FLAG1 == 3)
		IF(PLAYER1,ACTIVE_BATTLES >= 1)
			NEXT_COMMAND_REUSABLE
			SET_MUSIC("battle03.mp3")
			NEXT_COMMAND_REUSABLE
			SET_TIMER(PLAYER_NEUTRAL,TIMER0)
			NEXT_COMMAND_REUSABLE
			SET_FLAG(PLAYER_NEUTRAL,FLAG0,1)
		ENDIF
	ENDIF
ENDIF

IF(PLAYER_NEUTRAL,FLAG0 == 0)
	IF(PLAYER_NEUTRAL,FLAG1 == 4)
		IF(PLAYER1,ACTIVE_BATTLES >= 1)
			NEXT_COMMAND_REUSABLE
			SET_MUSIC("battle04.mp3")
			NEXT_COMMAND_REUSABLE
			SET_TIMER(PLAYER_NEUTRAL,TIMER0)
			NEXT_COMMAND_REUSABLE
			SET_FLAG(PLAYER_NEUTRAL,FLAG0,1)
		ENDIF
	ENDIF
ENDIF

IF(PLAYER_NEUTRAL,FLAG0 == 0)
	IF(PLAYER_NEUTRAL,FLAG1 == 5)
		IF(PLAYER1,ACTIVE_BATTLES >= 1)
			NEXT_COMMAND_REUSABLE
			SET_MUSIC("01. Theme.mp3")
			NEXT_COMMAND_REUSABLE
			SET_TIMER(PLAYER_NEUTRAL,TIMER0)
			NEXT_COMMAND_REUSABLE
			SET_FLAG(PLAYER_NEUTRAL,FLAG0,1)
		ENDIF
	ENDIF
ENDIF

IF(PLAYER_NEUTRAL,FLAG0 == 0)
	IF(PLAYER_NEUTRAL,FLAG1 == 6)
		IF(PLAYER1,ACTIVE_BATTLES >= 1)
			NEXT_COMMAND_REUSABLE
			SET_MUSIC("05. Outro Fail.mp3")
			NEXT_COMMAND_REUSABLE
			SET_TIMER(PLAYER_NEUTRAL,TIMER0)
			NEXT_COMMAND_REUSABLE
			SET_FLAG(PLAYER_NEUTRAL,FLAG0,1)
		ENDIF
	ENDIF
ENDIF

IF(PLAYER_NEUTRAL,FLAG0 == 0)
	IF(PLAYER_NEUTRAL,FLAG1 == 7)
		IF(PLAYER1,ACTIVE_BATTLES >= 1)
			NEXT_COMMAND_REUSABLE
			SET_MUSIC("06. Outro Win.mp3")
			NEXT_COMMAND_REUSABLE
			SET_TIMER(PLAYER_NEUTRAL,TIMER0)
			NEXT_COMMAND_REUSABLE
			SET_FLAG(PLAYER_NEUTRAL,FLAG0,1)
		ENDIF
	ENDIF
ENDIF

IF(PLAYER_NEUTRAL,FLAG0 == 1)
	IF(PLAYER_NEUTRAL,TIMER0 >= 600)
		IF(PLAYER1,ACTIVE_BATTLES == 0)	
			NEXT_COMMAND_REUSABLE
			RANDOMISE_FLAG(PLAYER_NEUTRAL,FLAG1,7)
			NEXT_COMMAND_REUSABLE
			SET_FLAG(PLAYER_NEUTRAL,FLAG0,0)
			NEXT_COMMAND_REUSABLE
			RANDOMISE_FLAG(PLAYER_NEUTRAL,FLAG2,4)
			NEXT_COMMAND_REUSABLE
			SET_TIMER(PLAYER_NEUTRAL,TIMER1)
			NEXT_COMMAND_REUSABLE
			ADD_TO_TIMER(PLAYER_NEUTRAL,TIMER1,6600)
		ENDIF
	ENDIF
ENDIF

IF(PLAYER_NEUTRAL,FLAG0 == 0)
	IF(PLAYER_NEUTRAL,FLAG2 == 1)
		IF(PLAYER_NEUTRAL,TIMER1 >= 6600)
			NEXT_COMMAND_REUSABLE
			SET_MUSIC(3)
			NEXT_COMMAND_REUSABLE
			ADD_TO_FLAG(PLAYER_NEUTRAL,FLAG2,1)
			NEXT_COMMAND_REUSABLE
			SET_TIMER(PLAYER_NEUTRAL,TIMER1)
		ENDIF
	ENDIF
ENDIF

IF(PLAYER_NEUTRAL,FLAG0 == 0)
	IF(PLAYER_NEUTRAL,FLAG2 == 2)
		IF(PLAYER_NEUTRAL,TIMER1 >= 6600)
			NEXT_COMMAND_REUSABLE
			SET_MUSIC(4)
			NEXT_COMMAND_REUSABLE
			ADD_TO_FLAG(PLAYER_NEUTRAL,FLAG2,1)
			NEXT_COMMAND_REUSABLE
			SET_TIMER(PLAYER_NEUTRAL,TIMER1)
		ENDIF
	ENDIF
ENDIF

IF(PLAYER_NEUTRAL,FLAG0 == 0)
	IF(PLAYER_NEUTRAL,FLAG2 == 3)
		IF(PLAYER_NEUTRAL,TIMER1 >= 6600)
			NEXT_COMMAND_REUSABLE
			SET_MUSIC(5)
			NEXT_COMMAND_REUSABLE
			ADD_TO_FLAG(PLAYER_NEUTRAL,FLAG2,1)
			NEXT_COMMAND_REUSABLE
			SET_TIMER(PLAYER_NEUTRAL,TIMER1)
		ENDIF
	ENDIF
ENDIF

IF(PLAYER_NEUTRAL,FLAG0 == 0)
	IF(PLAYER_NEUTRAL,FLAG2 == 4)
		IF(PLAYER_NEUTRAL,TIMER1 >= 6600)
			NEXT_COMMAND_REUSABLE
			SET_MUSIC(6)
			NEXT_COMMAND_REUSABLE
			ADD_TO_FLAG(PLAYER_NEUTRAL,FLAG2,-3)
			NEXT_COMMAND_REUSABLE
			SET_TIMER(PLAYER_NEUTRAL,TIMER1)
		ENDIF
	ENDIF
ENDIF

REM -------------------------------------------------------------------------------------------------------------------------------------------

QUICK_OBJECTIVE(0,"Your quest is almost over. The last of the Guardians, the Witch Queen, controls the final Heroic realm of Summerland. Kill and maim all you come across till there is no one left.",PLAYER1)

SET_TIMER(PLAYER3,TIMER3)

HIDE_HERO_GATE(-10,1)

IF(PLAYER1,CAMPAIGN_FLAG1 != 0)
	ADD_CREATURE_TO_POOL(ANGEL,2)
	CREATURE_AVAILABLE(PLAYER1,ANGEL,1,0)
ENDIF

IF(PLAYER1,CAMPAIGN_FLAG1 != 0)
	IF(PLAYER1,SCAVENGER > 1)
		ADD_CREATURE_TO_LEVEL(PLAYER_NEUTRAL,ANGEL,-10,1,8,0)
	ENDIF
ENDIF

IF(PLAYER1,GAME_TURN >= 1000)
	QUICK_INFORMATION(1,​"Summerland At War Status: Only a faint wind can be heard across the now silent battle lines. Victory seems a distant dream to the last Heroes. ",PLAYER1)
ENDIF

IF(PLAYER3,TIMER3 >= 15000)
	IF(PLAYER3,DUNGEON_DESTROYED == 0)
		IF(PLAYER3,TUNNELLER < 4)
			IF(PLAYER3,TOTAL_CREATURES < 20)
				NEXT_COMMAND_REUSABLE
				ADD_CREATURE_TO_LEVEL(PLAYER3,TUNNELLER,-1,1,5,0)
				NEXT_COMMAND_REUSABLE
				SET_TIMER(PLAYER3,TIMER3)
			ENDIF
		ENDIF
	ENDIF
ENDIF

IF(PLAYER3,DUNGEON_DESTROYED == 1)
	HIDE_HERO_GATE(-1,1)
ENDIF

IF(PLAYER1,GAME_TURN >= 1500)
	QUICK_INFORMATION(2,"Two castles are defending Saintsburgh; one in the west and a vastly larger one in the south. The Witch Queen will likely reside in the latter.",PLAYER1)
	TUTORIAL_FLASH_BUTTON(37,0)
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD,WITCH_QUEEN,2,1)
	CREATURE_AVAILABLE(PLAYER3,DWARFA,1,4)
	CREATURE_AVAILABLE(PLAYER3,ARCHER,1,2)
	CREATURE_AVAILABLE(PLAYER3,THIEF,1,2)
	CREATURE_AVAILABLE(PLAYER3,BARBARIAN,1,2)
	CREATURE_AVAILABLE(PLAYER3,GIANT,1,4)
	CREATURE_AVAILABLE(PLAYER3,FAIRY,1,2)
	CREATURE_AVAILABLE(PLAYER3,MONK,1,2)
	CREATURE_AVAILABLE(PLAYER3,WIZARD,1,2)
	CREATURE_AVAILABLE(PLAYER3,SAMURAI,1,4)
	CREATURE_AVAILABLE(PLAYER3,KNIGHT,1,30)
	CREATURE_AVAILABLE(PLAYER3,AVATAR,1,10)
	CREATURE_AVAILABLE(PLAYER3,TIME_MAGE,1,2)
ENDIF

IF(PLAYER1,TOTAL_CREATURES >= 10)
	QUICK_INFORMATION(3,"Undead Warriors have been sighted in this realm, especially in the west. They appear to be unaligneded. Mind you, going in that direction will get you dangerously close to the western castle.",PLAYER1)
ENDIF

IF(PLAYER1,VAMPIRE >= 1)
	QUICK_MESSAGE(4, "Your aid is most appreciated... Keeper.", Vampire)
ENDIF

IF(PLAYER1,ENTRANCE == 18)
	MAX_CREATURES(PLAYER1,25)
	CREATURE_ENTRANCE_LEVEL(PLAYER1,3)
	SET_GENERATE_SPEED(575)
	QUICK_INFORMATION(5,"Creatures from your previous conquests are making their way towards you to join in this great last battle.",PLAYER1)
ENDIF

IF(PLAYER1,ENTRANCE == 27)
	MAX_CREATURES(PLAYER1,30)
	CREATURE_ENTRANCE_LEVEL(PLAYER1,5)
	SET_GENERATE_SPEED(550)
	QUICK_INFORMATION(6,"More minions from all corners of the continent are eagerly flocking towards your side. They know ultimate victory is near.",PLAYER1)
ENDIF

IF(PLAYER1,ENTRANCE == 36)
	MAX_CREATURES(PLAYER1,40)
	CREATURE_ENTRANCE_LEVEL(PLAYER1,7)
	SET_GENERATE_SPEED(500)
	QUICK_INFORMATION(7,"Your army is reaching its maximum size. Elite Creatures have arrived at the outskirts of this realm and are ready to replace the fallen.",PLAYER1)
ENDIF

IF_ACTION_POINT(8,PLAYER1)
    QUICK_OBJECTIVE(8,"You are approaching the one of the two castles defending Saintsburgh with the Witch Queen residing in this one. Assault the place when you are ready for it.",PLAYER1)
ENDIF

IF_ACTION_POINT(8,PLAYER1)
	IF(PLAYER3,DUNGEON_DESTROYED == 0)
		QUICK_INFORMATION(9,"You have not yet destroyed the western castle. Perhaps it would be wise to secure your flanks first before moving on to the main prize.",PLAYER3)
	ENDIF
ENDIF

IF_ACTION_POINT(1,PLAYER1)
    SET_TIMER(PLAYER3,TIMER0)
    SET_FLAG(PLAYER3,FLAG0,1)
ENDIF

IF(PLAYER3,FLAG0 == 1)
    IF(PLAYER3,DUNGEON_DESTROYED == 0)
		IF(PLAYER_GOOD,DUNGEON_DESTROYED == 0)
			IF(PLAYER3,TIMER0 >= 7500)
				NEXT_COMMAND_REUSABLE
				ADD_PARTY_TO_LEVEL(PLAYER_GOOD,BLUE_ONE,-1,1)
				NEXT_COMMAND_REUSABLE
				SET_TIMER(PLAYER3,TIMER0)
			ENDIF
		ENDIF
	ENDIF
ENDIF

IF(PLAYER3,FLAG0 == 2)
    IF(PLAYER3,DUNGEON_DESTROYED == 0)
			IF(PLAYER_GOOD,DUNGEON_DESTROYED == 0)
				IF(PLAYER3,TIMER1 >= 7000)
				NEXT_COMMAND_REUSABLE
				ADD_PARTY_TO_LEVEL(PLAYER_GOOD,BLUE_TWO,-1,1)
				NEXT_COMMAND_REUSABLE
				SET_TIMER(PLAYER3,TIMER1)
			ENDIF
		ENDIF
	ENDIF
ENDIF

IF(PLAYER3,FLAG0 == 3)
    IF(PLAYER3,DUNGEON_DESTROYED == 0)
			IF(PLAYER_GOOD,DUNGEON_DESTROYED == 0)
				IF(PLAYER3,TIMER2 >= 6500)
				NEXT_COMMAND_REUSABLE
				ADD_PARTY_TO_LEVEL(PLAYER_GOOD,ASSASSIN,-1,1)
				NEXT_COMMAND_REUSABLE
				ADD_PARTY_TO_LEVEL(PLAYER_GOOD,BLUE_THREE,-1,1)
				NEXT_COMMAND_REUSABLE
				SET_TIMER(PLAYER3,TIMER2)
			ENDIF
		ENDIF
	ENDIF
ENDIF

IF_ACTION_POINT(3,PLAYER1)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,RAIDERS,-6,1)
    SET_TIMER(PLAYER_GOOD,TIMER0)
	MAGIC_AVAILABLE(PLAYER3,POWER_SPEED,1,1)
	MAGIC_AVAILABLE(PLAYER3,POWER_HEAL_CREATURE,1,1)
	MAGIC_AVAILABLE(PLAYER3,POWER_PROTECT,1,1)
    SET_FLAG(PLAYER_GOOD,FLAG0,1)
	QUICK_OBJECTIVE(10,"The Witch Queen has bestowed protective blessings on her fellow Heroes. Unfortunately for them it is a little too late at this point.")
    SET_CREATURE_ARMOUR(AVATAR,80)
	SET_CREATURE_ARMOUR(FAIRY,30)
    SET_CREATURE_ARMOUR(WIZARD,30)
	SET_CREATURE_ARMOUR(TIME_MAGE,30)
    SET_CREATURE_ARMOUR(ARCHER,40)
    SET_CREATURE_ARMOUR(MONK,45)
    SET_CREATURE_ARMOUR(THIEF,40)
    SET_CREATURE_ARMOUR(DWARFA,50)
    SET_CREATURE_ARMOUR(BARBARIAN,60)
    SET_CREATURE_ARMOUR(SAMURAI,70)
    SET_CREATURE_ARMOUR(GIANT,70)
    SET_CREATURE_ARMOUR(KNIGHT,120)
	MAX_CREATURES(PLAYER3,22)
	CREATURE_ENTRANCE_LEVEL(PLAYER3,3)
	ADD_GOLD_TO_PLAYER(PLAYER3,100000)
ENDIF

IF(PLAYER_GOOD,FLAG0 == 1)
    IF(PLAYER_GOOD,TIMER0 >= 6000)
        NEXT_COMMAND_REUSABLE
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,RAIDERS,-6,1)
        NEXT_COMMAND_REUSABLE
		ADD_PARTY_TO_LEVEL(PLAYER_GOOD,FAIRY_LIGHT,-9,1)
        NEXT_COMMAND_REUSABLE
		ADD_PARTY_TO_LEVEL(PLAYER_GOOD,GIANT_LIGHT,-8,1)
        NEXT_COMMAND_REUSABLE
        SET_TIMER(PLAYER_GOOD,TIMER0)
    ENDIF
ENDIF

IF_ACTION_POINT(4,PLAYER1)
	MAGIC_AVAILABLE(PLAYER3,POWER_CHICKEN,1,1)
    QUICK_OBJECTIVE(11,"The Witch Queen is casting curses on your creatures and additional blessings on her fellow Heroes to slow your advance.")
    SET_TIMER(PLAYER_GOOD,TIMER1)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,RAIDERS,-4,1)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,RAIDERS,-5,1)
	MAX_CREATURES(PLAYER3,25)
	CREATURE_ENTRANCE_LEVEL(PLAYER3,5)
	USE_POWER_ON_CREATURE(PLAYER1,ANY_CREATURE,ON_ENEMY_GROUND,PLAYER_GOOD,POWER_CHICKEN,9,0)
	USE_SPELL_ON_CREATURE(PLAYER1,ANY_CREATURE,ON_ENEMY_GROUND,SPELL_FREEZE,0)
	SET_FLAG(PLAYER1,FLAG0,1)
	SET_TIMER(PLAYER1,TIMER1)
	SET_TIMER(PLAYER1,TIMER2)
	SET_TIMER(PLAYER1,TIMER3)
	SET_TIMER(PLAYER1,TIMER4)
	ADD_GOLD_TO_PLAYER(PLAYER3,100000)
	SET_PLAYER_MODIFIER(PLAYER_GOOD,Speed,115)
	SET_PLAYER_MODIFIER(PLAYER3,Speed,115)
	SET_PLAYER_MODIFIER(PLAYER_GOOD,Health,125)
	SET_PLAYER_MODIFIER(PLAYER3,Health,125)
	SET_PLAYER_MODIFIER(PLAYER3,SALARY,50)
	SET_PLAYER_MODIFIER(PLAYER3,TrainingCost,50)
ENDIF

IF(PLAYER1,FLAG0 == 1)
    IF(PLAYER1,TIMER1 >= 475)
		NEXT_COMMAND_REUSABLE
		USE_POWER_ON_CREATURE(PLAYER_GOOD,ANY_CREATURE,LEAST_EXP_FIGHTING,PLAYER_GOOD,POWER_SPEED,9,0)
		NEXT_COMMAND_REUSABLE
		SET_TIMER(PLAYER1,TIMER1)
	 ENDIF
ENDIF

IF(PLAYER1,FLAG0 == 1)
    IF(PLAYER1,TIMER2 >= 425)
		NEXT_COMMAND_REUSABLE
		USE_POWER_ON_CREATURE(PLAYER_GOOD,ANY_CREATURE,LEAST_EXP_FIGHTING,PLAYER_GOOD,POWER_PROTECT,9,0)
		NEXT_COMMAND_REUSABLE
		SET_TIMER(PLAYER1,TIMER2)
	 ENDIF
ENDIF

IF(PLAYER1,FLAG0 == 1)
    IF(PLAYER1,TIMER3 >= 400)
		NEXT_COMMAND_REUSABLE
		USE_SPELL_ON_CREATURE(PLAYER1,ANY_CREATURE,ON_ENEMY_GROUND,SPELL_FREEZE,0)
		NEXT_COMMAND_REUSABLE
		SET_TIMER(PLAYER1,TIMER3)
	 ENDIF
ENDIF

IF(PLAYER1,FLAG0 == 1)
    IF(PLAYER1,TIMER4 >= 350)
		NEXT_COMMAND_REUSABLE
		USE_POWER_ON_CREATURE(PLAYER1,ANY_CREATURE,ON_ENEMY_GROUND,PLAYER_GOOD,POWER_CHICKEN,9,0)
		NEXT_COMMAND_REUSABLE
		SET_TIMER(PLAYER1,TIMER4)
	 ENDIF
ENDIF

IF_ACTION_POINT(4,PLAYER1)
    IF_ACTION_POINT(1,PLAYER1)
		ADD_TO_FLAG(PLAYER3,FLAG0,1)
        SET_TIMER(PLAYER3,TIMER1)
    ENDIF
ENDIF

IF_ACTION_POINT(5,PLAYER1)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,BRUTES,-7,1)
ENDIF

IF_ACTION_POINT(6,PLAYER1)
    SET_TIMER(PLAYER_GOOD,TIMER1)
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD,HOLY,-3,1)
	SET_FLAG(PLAYER_GOOD,FLAG0,2)
	HIDE_HERO_GATE(-6,1)
ENDIF

IF_ACTION_POINT(6,PLAYER1)
	IF(PLAYER3,DUNGEON_DESTROYED == 1)
		ROOM_AVAILABLE(ALL_PLAYERS,BRIDGE,1,1)
		TUTORIAL_FLASH_BUTTON(18,100)
		QUICK_INFORMATION(12,"Your Creatures have researched the Bridge. You can now construct it to reach previously inaccessible places.",PLAYER1)
	ENDIF
ENDIF

IF(PLAYER_GOOD,FLAG0 == 2)
    IF(PLAYER_GOOD,TIMER1 >= 5000)
        NEXT_COMMAND_REUSABLE
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,HOLY,-3,1)
        NEXT_COMMAND_REUSABLE
		ADD_PARTY_TO_LEVEL(PLAYER_GOOD,FAIRY_GOLD_THIEF,-9,1)
        NEXT_COMMAND_REUSABLE
		ADD_PARTY_TO_LEVEL(PLAYER_GOOD,FAIRY_SPELL_THIEF,-8,1)
        NEXT_COMMAND_REUSABLE
		ADD_PARTY_TO_LEVEL(PLAYER_GOOD,GIANT_ATTACKER,-9,1)
        NEXT_COMMAND_REUSABLE
		ADD_PARTY_TO_LEVEL(PLAYER_GOOD,GIANT_DESTROYER,-8,1)
        NEXT_COMMAND_REUSABLE
        SET_TIMER(PLAYER_GOOD,TIMER1)
    ENDIF
ENDIF

REM Endgame

IF_ACTION_POINT(7,PLAYER1)
    SET_FLAG(PLAYER_GOOD,FLAG0,3)
	SET_FLAG(PLAYER3,FLAG0,3)
    SET_TIMER(PLAYER_GOOD,TIMER2)
	SET_TIMER(PLAYER3,TIMER2)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,RPG,-2,1)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,SCOUTS,-3,1)
    QUICK_OBJECTIVE(13,"The Heroes are drawing on their final forces in a last ditch effort to push you back. Pathetic.")
	MAX_CREATURES(PLAYER3,25)
	CREATURE_ENTRANCE_LEVEL(PLAYER3,4)
	ADD_GOLD_TO_PLAYER(PLAYER3,100000)
	MAGIC_AVAILABLE(PLAYER_GOOD,POWER_DISEASE,1,1)
	HIDE_HERO_GATE(-3,1)
	HIDE_HERO_GATE(-4,1)
	HIDE_HERO_GATE(-5,1)
	ZOOM_TO_LOCATION(PLAYER1,2)
ENDIF

IF(PLAYER_GOOD,TIMER2 >= 900)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,SCOUTS,-2,1)
	QUICK_MESSAGE(14, "Pestilence upon you!", WITCH)
	USE_POWER_ON_CREATURE(PLAYER1,ANY_CREATURE,ON_ENEMY_GROUND,PLAYER_GOOD,POWER_DISEASE,9,0)
ENDIF

IF(PLAYER_GOOD,TIMER2 >= 1800)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,BRUTES,-2,1)
	USE_POWER_ON_CREATURE(PLAYER1,ANY_CREATURE,ON_ENEMY_GROUND,PLAYER_GOOD,POWER_DISEASE,9,0)
ENDIF

IF(PLAYER_GOOD,TIMER2 >= 3000)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,ELITE,-2,1)
	USE_POWER_ON_CREATURE(PLAYER1,ANY_CREATURE,ON_ENEMY_GROUND,PLAYER_GOOD,POWER_DISEASE,9,0)
ENDIF

IF(PLAYER_GOOD,TIMER2 >= 4000)
	MAGIC_AVAILABLE(PLAYER3,POWER_DISEASE,1,1)
    SET_TIMER(PLAYER_GOOD,TIMER3)
    SET_TIMER(PLAYER_GOOD,TIMER4)
	SET_TIMER(PLAYER_GOOD,TIMER5)
	SET_TIMER(PLAYER_GOOD,TIMER6)
    MAX_CREATURES(PLAYER3,25)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,BRUTES,-7,1)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,SCOUTS,-2,1)
	CREATURE_ENTRANCE_LEVEL(PLAYER3,5)
	USE_POWER_ON_CREATURE(PLAYER1,ANY_CREATURE,ON_ENEMY_GROUND,PLAYER_GOOD,POWER_DISEASE,9,0)
ENDIF

IF(PLAYER_GOOD,TIMER2 >= 15000)
	ADD_TO_PARTY(ELITE,FAIRY,10,1500,DEFEND_PARTY,0)
	ADD_TO_PARTY(SCOUTS,DWARFA,8,500,ATTACK_ROOMS,0)
	ADD_TO_PARTY(RPG,FAIRY,9,1000,DEFEND_PARTY,1600)
ENDIF

IF(PLAYER_GOOD,TIMER2 >= 30000)
	ADD_TO_PARTY(ELITE,MONK,10,1500,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(RPG,GIANT,10,800,DEFEND_PARTY,1600)
	ADD_TO_PARTY(SCOUTS,THIEF,9,100,STEAL_GOLD,0)
ENDIF

IF(PLAYER_GOOD,TIMER2 >= 45000)
	ADD_TO_PARTY(ELITE,TIME_MAGE,10,1500,DEFEND_PARTY,0)
	ADD_TO_PARTY(SCOUTS,DWARFA,9,500,ATTACK_ROOMS,0)
ENDIF

IF(PLAYER_GOOD,TIMER3 >= 8000)
    IF(PLAYER_GOOD,TOTAL_CREATURES <= 8)
        IF(PLAYER_GOOD,WITCH == 1)
			NEXT_COMMAND_REUSABLE
			ADD_PARTY_TO_LEVEL(PLAYER_GOOD,SPELL_THIEF,-2,1)
			NEXT_COMMAND_REUSABLE
			ADD_PARTY_TO_LEVEL(PLAYER_GOOD,THIEF_GOLD,-2,1)
			NEXT_COMMAND_REUSABLE
			ADD_PARTY_TO_LEVEL(PLAYER_GOOD,THIEF_SPELL,-2,1)
            NEXT_COMMAND_REUSABLE
            ADD_PARTY_TO_LEVEL(PLAYER_GOOD,RPG,-2,1)
			NEXT_COMMAND_REUSABLE
			SET_TIMER(PLAYER_GOOD,TIMER3)
        ENDIF
    ENDIF
ENDIF

IF(PLAYER_GOOD,TIMER4 >= 5000)
    IF(PLAYER_GOOD,TOTAL_CREATURES <= 36)
        IF(PLAYER_GOOD,WITCH == 1)
            IF(PLAYER_GOOD,DUNGEON_DESTROYED == 0)
				NEXT_COMMAND_REUSABLE
                ADD_PARTY_TO_LEVEL(PLAYER_GOOD,ASSASSIN,-2,1)
                NEXT_COMMAND_REUSABLE
                ADD_PARTY_TO_LEVEL(PLAYER_GOOD,ELITE,-2,1)
				NEXT_COMMAND_REUSABLE
				ADD_PARTY_TO_LEVEL(PLAYER_GOOD,SCOUTS,-2,1)
				NEXT_COMMAND_REUSABLE
                SET_TIMER(PLAYER_GOOD,TIMER4)
            ENDIF
        ENDIF
    ENDIF
ENDIF

IF(PLAYER_GOOD,TIMER5 >= 5000)
    IF(PLAYER_GOOD,TOTAL_CREATURES <= 36)
        IF(PLAYER_GOOD,WITCH == 1)
            IF(PLAYER_GOOD,DUNGEON_DESTROYED == 0)
				IF_SLAB_OWNER(72,​73,​PLAYER_GOOD)
					NEXT_COMMAND_REUSABLE
					ADD_PARTY_TO_LEVEL(PLAYER_GOOD,FAIRY_GOLD_THIEF,-9,1)
					NEXT_COMMAND_REUSABLE
					ADD_PARTY_TO_LEVEL(PLAYER_GOOD,GIANT_ATTACKER,-9,2)	
					NEXT_COMMAND_REUSABLE
					SET_TIMER(PLAYER_GOOD,TIMER5)
				ENDIF
			ENDIF
		ENDIF
	ENDIF
ENDIF

IF_SLAB_OWNER(72,​73,​PLAYER1)
	HIDE_HERO_GATE(-9,1)
ENDIF

IF(PLAYER_GOOD,TIMER6 >= 5000)
    IF(PLAYER_GOOD,TOTAL_CREATURES <= 36)
        IF(PLAYER_GOOD,WITCH == 1)
            IF(PLAYER_GOOD,DUNGEON_DESTROYED == 0)
				IF_SLAB_OWNER(18,​72,​PLAYER_GOOD)
					NEXT_COMMAND_REUSABLE
					ADD_PARTY_TO_LEVEL(PLAYER_GOOD,FAIRY_SPELL_THIEF,-8,1)
					NEXT_COMMAND_REUSABLE
					ADD_PARTY_TO_LEVEL(PLAYER_GOOD,GIANT_DESTROYER,-8,2)
					NEXT_COMMAND_REUSABLE
					SET_TIMER(PLAYER_GOOD,TIMER6)
				ENDIF
			ENDIF
		ENDIF
	ENDIF
ENDIF

IF_SLAB_OWNER(18,​72,​PLAYER1)
	HIDE_HERO_GATE(-8,1)
ENDIF

IF(PLAYER_GOOD,FLAG0 == 3)
    IF(PLAYER_GOOD,WITCH == 0)
		QUICK_OBJECTIVE(15,"You have done it! The last of the Guardians is dead! Now no one can stop you!")
        MAGIC_AVAILABLE(PLAYER_GOOD,POWER_SPEED,0,0)
        MAGIC_AVAILABLE(PLAYER_GOOD,POWER_CHICKEN,0,0)
		MAGIC_AVAILABLE(PLAYER_GOOD,POWER_PROTECT,0,0)
        MAGIC_AVAILABLE(PLAYER_GOOD,POWER_DISEASE,0,0)
		MAGIC_AVAILABLE(PLAYER3,POWER_DISEASE,0,0)
		MAGIC_AVAILABLE(PLAYER3,POWER_CHICKEN,0,0)
		MAGIC_AVAILABLE(PLAYER3,POWER_SPEED,0,0)
		MAGIC_AVAILABLE(PLAYER3,POWER_HEAL_CREATURE,0,0)
		MAGIC_AVAILABLE(PLAYER3,POWER_PROTECT,0,0)
        SET_FLAG(PLAYER_GOOD,FLAG0,4)
		ADD_TO_FLAG(PLAYER1,FLAG0,-1)
		HIDE_HERO_GATE(-8,1)
		HIDE_HERO_GATE(-9,1)
		HIDE_HERO_GATE(-2,1)
    ENDIF
ENDIF

IF(PLAYER1,ALL_DUNGEONS_DESTROYED == 1)
    IF(PLAYER_GOOD,FLAG0 == 4)
		IF(PLAYER_GOOD,TOTAL_CREATURES == 0)
			WIN_GAME
			QUICK_MESSAGE(16, "A force unmatched. My name, a Nemesis in their minds.", PLAYER1)
		ENDIF
	ENDIF
ENDIF